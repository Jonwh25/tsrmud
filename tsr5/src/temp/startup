#! /bin/csh -f
# Written by Furey.
# With additions from Tony.
# With changes from Kahn.

# Set the port number.
set port = 9000
if ( "$1" != "" ) set port="$1"

# Change to area directory.
cd ../area

# Set limits.
nohup
limit stack 1024k
if ( -e shutdown.txt ) rm shutdown.txt

while ( 1 )
    # If you want to have logs in a different directory,
    #   change the 'set logfile' line to reflect the directory name.
    set index = 1000
    while ( 1 )
	set logfile = ../log/$index.log
	if ( ! -e $logfile ) break
	@ index++
    end

    # Update to new code if possible.
    if ( -e ../src/eos.new ) then
	\mv ../src/eos ../src/eos.old
	\mv ../src/eos.new ../src/eos
    endif

    # Run envy.
    ../src/eos $port >&! $logfile

    if ( -e core ) then
	\mv core ../src/
    endif

    # Restart, giving old connections a chance to die.
    if ( -e shutdown.txt ) then
	rm shutdown.txt
	exit 0
    endif

    sleep 10
end
